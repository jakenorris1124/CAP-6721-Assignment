#version 430 core
layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in; // You may use x and y size to be different from 1. In such a case adjust the workgroup numbers during shader dispatch time

// Declare an image2D variable to represent a viewport raster image
layout (rgba32f, binding = 0) uniform image2D outputImage;

// Define a Ray structure
struct Ray {
    vec3 origin;
    vec3 direction;
    float tmin, tmax;
    int n;
};

struct Sphere {
    vec4 color;
    vec3 center;
    float radius;
};

struct Box {
    vec4 minimum;
    vec4 maximum;
};

struct SphereIntersection
{
    float t;
    Sphere hit;
};

struct BoxIntersection
{
    float t;
    int hit;
    vec3 normal;
};

struct TraceResult
{
    float t;
    vec4 color;
};

const int RAY_HIT_NONE = -1;
const int RAY_FLAG_CAMERA = 1;
const int RAY_FLAG_SHADOW = 2;
const vec4 IN_LIGHT = vec4(1, 1, 1, 1);
const vec4 IN_SHADOW = vec4(0, 0, 0, 0);


layout (std430, binding = 10) readonly buffer boundsBuffer
{
    Box bounds;
};

layout (std430, binding = 11) readonly buffer sphereBuffer
{
    Sphere spheres[];
};

/*
* Index 0 -> Left wall
* Index 1 -> Right wall
* Index 2 -> Top wall
* Index 3 -> Bottom wall
* Index 4 -> Back wall
*/
layout (std430, binding = 20) readonly buffer wallBuffer
{
    mat4 wallMatrices[];
};

// Inside the compute shader kernel:
uniform vec3 eye, at, up, light;
uniform float fov;
uniform vec2 resolution;
uniform int NUM_SAMPLES;

bool aabbHit(Ray ray, Box aabb);

bool validT(float t, Ray ray)
{
    return t > ray.tmin && t < ray.tmax;
}

vec4 missShader(Ray ray) 
{
    vec3 unit_direction = normalize(ray.direction);
    float a = 0.5*(unit_direction.y + 1.0);
    vec3 color = (1.0-a)*vec3(1.0, 1.0, 1.0) + a*vec3(0.5, 0.7, 1.0);

    return vec4(color.xyz, 1.0);
}

vec4 hitShader(Ray ray, float t, Sphere sphere)
{
    vec3 intersectionPoint = ray.origin + (t * ray.direction);
    vec3 sphereNormal = normalize(intersectionPoint - sphere.center);

    float angle = dot(normalize(eye), sphereNormal);

    return vec4(sphereNormal, 1.0);
}

vec4 boxHitShader(vec3 color, vec3 normal)
{
    float angle = dot(normalize(eye), normal);

    if(angle < 0.9)
        angle = 0.5;

    return vec4(color, 1) * angle;
}

float sphereHit(Sphere sphere, Ray ray)
{
    vec3 oc = ray.origin - sphere.center;
    float a = dot(ray.direction, ray.direction);
    float b = 2.0 * dot(ray.direction, oc);
    float c = dot(oc, oc) - (sphere.radius * sphere.radius);
    float discriminant = (b * b) - (4.0 * a * c);

    if (discriminant < 0.0)
        return -1.0;

    discriminant = sqrt(discriminant);
    float t = (-b - discriminant)  / (2.0 * a);

    if (t <= ray.tmin)
        t = (-b + discriminant)  / (2.0 * a);

    if (t > ray.tmin && t < ray.tmax)
        return t;
        
    return -1.0;
}

float optimizedSphereHit(Sphere sphere, Ray ray)
{
    vec3 co = sphere.center - ray.origin;
    float l2 = dot(co, co);
    float r2 = sphere.radius * sphere.radius;

    float s  = dot(co, ray.direction);
    float m2 = l2 - (s * s);
    
    if (m2 <= r2)
    {
        float q = sqrt(r2 - m2);
        return s - q;
    }

    return -1.0;
}

SphereIntersection intersectAllSpheres(Ray ray, int source)
{
    // Return early if AABB of molecule not hit
    SphereIntersection payload;
    if(!aabbHit(ray, bounds))
    {
        payload.t = RAY_HIT_NONE;
        return payload;
    }

    // Check ray intersection with all spheres
    int numSpheres = spheres.length();
    float closestT = ray.tmax;
    int closestI = -1;
    for (int i  = 0; i < numSpheres; i++)
    {
        float intermediateT = sphereHit(spheres[i], ray);
        if (validT(intermediateT, ray) && intermediateT < closestT)
        {
            // Return early if intersection found and the ray is a shadow ray
            if (source == RAY_FLAG_SHADOW)
            {
                payload.t = intermediateT;
                return payload;
            }

            closestT = intermediateT;
            closestI = i;
        }
    }

    // Return with no hit 
    if(closestI == -1 || !validT(closestT, ray))
    {
        payload.t = RAY_HIT_NONE;
        return payload;
    }

    // Return with hit
    payload.t = closestT;
    payload.hit = spheres[closestI];
    return payload;
}

bool aabbHit(Ray ray, Box aabb)
{
    vec3 tMin = (aabb.minimum.xyz - ray.origin) / ray.direction;
    vec3 tMax = (aabb.maximum.xyz - ray.origin) / ray.direction;

    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);

    float tNear = max(max(max(t1.x, t1.y), t1.z), ray.tmin);
    float tFar = min(min(min(t2.x, t2.y), t2.z), ray.tmax);

    return tNear <= tFar;
}

vec3 aabbHitPoint(Ray ray, Box aabb)
{
    vec3 tMin = (aabb.minimum.xyz - ray.origin) / ray.direction;
    vec3 tMax = (aabb.maximum.xyz - ray.origin) / ray.direction;

    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);

    float tNear = max(max(max(t1.x, t1.y), t1.z), ray.tmin);
    float tFar = min(min(min(t2.x, t2.y), t2.z), ray.tmax);

    if (tNear <= tFar)
        return ray.origin + (t2 * ray.direction);

    return ray.origin;
}

vec3 aabbHitNormal(Ray ray, Box aabb)
{
    vec3 tMin = (aabb.minimum.xyz - ray.origin) / ray.direction;
    vec3 tMax = (aabb.maximum.xyz - ray.origin) / ray.direction;

    vec3 t1 = min(tMin, tMax);
    float dir = 1.0;
    if (tMin == t1)
        dir = -1.0;

    float tNear = max(max(t1.x, t1.y), t1.z);

    if (t1.x == tNear)
        return vec3(dir, 0, 0);
    if (t1.y == tNear)
        return vec3(0, dir, 0);
    return vec3(0, 0, dir);
}

BoxIntersection intersectAllBoxes(Ray ray, int source)
{
   BoxIntersection boxI;

   Box canonical;
   canonical.minimum = vec4(-1, -1, -1, 1);
   canonical.maximum = vec4(1, 1, 1, 1);
   float closestT = ray.tmax;
   int closestI = -1;
   vec3 normal;
   for (int i = 0; i < 5; i++)
   {
        Ray wallRay = ray;
        wallRay.origin = (inverse(wallMatrices[i]) * vec4(ray.origin, 1)).xyz;
        wallRay.direction = (inverse(wallMatrices[i]) * vec4(ray.direction, 0)).xyz;

        vec3 point = aabbHitPoint(wallRay, canonical);
        if(point != wallRay.origin)
        {
            point = (wallMatrices[i] * vec4(point, 1)).xyz;
            float t = distance(point, ray.origin);

            if (t > ray.tmin && t < closestT)
            {
               if(source == RAY_FLAG_SHADOW)
               {
                    boxI.t = t;
                    return boxI;
               }

               closestT = t;
               closestI = i;

               mat4 normalMatrix = transpose(inverse(wallMatrices[i]));
               vec3 objectNormal = aabbHitNormal(wallRay, canonical);
               normal = normalize((normalMatrix * vec4(objectNormal, 0)).xyz);
               //normal = aabbHitNormal(wallRay, canonical);
            }
        }
   }

   if(closestI == -1 || !validT(closestT, ray))
   {
        boxI.t = RAY_HIT_NONE;
        return boxI;
   }

   boxI.t = closestT;
   boxI.hit = closestI;
   boxI.normal = normal;
   return boxI;
}

TraceResult traceRay(Ray ray, int source) 
{
    TraceResult trace;
    SphereIntersection spherePayload = intersectAllSpheres(ray, source);
    if(source == RAY_FLAG_SHADOW)
    {
        if(spherePayload.t == RAY_HIT_NONE)
            trace.color = IN_LIGHT;
        else
            trace.color = IN_SHADOW;
        return trace;
    }

    BoxIntersection boxPayload = intersectAllBoxes(ray, source);
    if(source == RAY_FLAG_SHADOW)
    {
        if(boxPayload.t == RAY_HIT_NONE)
            trace.color = IN_LIGHT;
        else
            trace.color = IN_SHADOW;
        return trace;
    }

    if(spherePayload.t != RAY_HIT_NONE && spherePayload.t < boxPayload.t)
    {
         vec3 hitPoint = ray.origin + (spherePayload.t * ray.direction);
         trace.color = hitShader(ray, spherePayload.t, spherePayload.hit);
         trace.t = spherePayload.t;
         return trace;
    }
    else if(boxPayload.t != RAY_HIT_NONE)
    {
        vec4 color;
        if(boxPayload.hit == 0)
            color = vec4(1, 0, 0, 1);
        if(boxPayload.hit == 1)
            color = vec4(0, 0, 1, 1);
        if(boxPayload.hit == 2 || boxPayload.hit == 3)
            color = vec4(0, 1, 0, 1);
        if(boxPayload.hit == 4)
            color = vec4(1, 1, 1, 1);

         trace.color = boxHitShader(color.xyz, boxPayload.normal);
         trace.t = boxPayload.t;
         return trace;
    }

   trace.color = missShader(ray);
   trace.t = RAY_HIT_NONE;
   return trace;
}

Ray rayGenerate(vec2 pixel, int n) 
{
    Ray ray;
    vec3 w = normalize(eye-at);
    vec3 u = normalize(cross(up,w));
    vec3 v = cross(w,u);
    float height = 2.0*tan(fov/2.0);
    float width = height * resolution.x/ resolution.y;
    ray.origin = eye;
    ray.direction = normalize(-w 
    + ((1 - ((2.0 * pixel.y + 1) / resolution.y)) * (height / 2.0) * v)
    - ((1 - ((2.0 * pixel.x + 1) / resolution.x)) * (width / 2.0) * u)
    ); 
    ray.tmax = 1.0 / 0.0;
    ray.tmin = 0.0;

    ray.n = n;
    return ray;
}

Ray shadowRayGenerate(Ray ray, float t)
{
    Ray shadow;
    shadow.origin = ray.origin + (t * ray.direction);
    shadow.direction = light - shadow.origin;
    shadow.tmin = 0.01;
    shadow.tmax = length(shadow.direction);

    return shadow;
}

vec4 sampleRay(vec2 pixel, int n)
{
    Ray ray = rayGenerate(pixel, n);
    TraceResult primary = traceRay(ray, RAY_FLAG_CAMERA);

    vec4 color = primary.color;
    if(primary.t != RAY_HIT_NONE)
    {
        Ray shadow = shadowRayGenerate(ray, primary.t);
        TraceResult secondary = traceRay(shadow, RAY_FLAG_SHADOW);
        color = secondary.color * primary.color;
    }

    return color;
}

void main() {

    // Get thread coordinates
    vec2 pixel = gl_GlobalInvocationID.xy;

    // trace NUM_SAMPLES rays for pixel(x,y)
    vec4 accumulate = vec4(0);
    for (int i = 0; i < NUM_SAMPLES; i++)
    {
        accumulate += sampleRay(pixel, i);
    }
    accumulate /= NUM_SAMPLES;
    

    // Write the calculated color to the output image
    imageStore(outputImage, ivec2(pixel.x, pixel.y), accumulate);
}

